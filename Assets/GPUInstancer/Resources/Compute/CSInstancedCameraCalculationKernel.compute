
#if SHADER_API_MOBILE && SHADER_API_METAL
    #define NUM_THREADS 512
#elif SHADER_API_MOBILE && SHADER_API_GLES3
    #define NUM_THREADS 128
#elif SHADER_API_MOBILE && SHADER_API_VULKAN
    #define NUM_THREADS 128
#elif SHADER_API_GLCORE
    #define NUM_THREADS 512
#else
    #define NUM_THREADS 1024
#endif

#pragma kernel CSInstancedCameraCalculationKernel
#pragma kernel CSInstancedCameraCalculationKernelCrossFade

RWStructuredBuffer<uint4> gpuiInstanceLODData; // lodNo - shadowLodNo - cfLodNo - cfLevel 
#include "Include/Culling.compute"
#include "Include/Camera.compute"

[numthreads(NUM_THREADS, 1, 1)]
void CSInstancedCameraCalculationKernel(uint3 id : SV_DispatchThreadID)
{
    if (id.x >= bufferSize)
        return;

    float dist = 0;
    bool culled = false;
    float size = 0;

    uint lodNo = 9;
    uint shadowLodNo = 9;
    uint cfLodNo = 9;
    uint fadeLevel = 0;

    if (IsAppending(id, dist, culled, size))
    {
        CalculateLODNo(size, dist, culled, lodNo, shadowLodNo);
    }

    gpuiInstanceLODData[id.x].x = lodNo;
    gpuiInstanceLODData[id.x].y = shadowLodNo;
    gpuiInstanceLODData[id.x].z = cfLodNo;
    gpuiInstanceLODData[id.x].w = fadeLevel;
}

[numthreads(NUM_THREADS, 1, 1)]
void CSInstancedCameraCalculationKernelCrossFade(uint3 id : SV_DispatchThreadID)
{
    if (id.x >= bufferSize)
        return;

    float dist = 0;
    bool culled = false;
    float size = 0;

    uint lodNo = 9;
    uint shadowLodNo = 9;
    uint cfLodNo = 9;
    uint fadeLevel = 0;

    if (IsAppending(id, dist, culled, size))
    {
        CalculateLODNo(size, dist, culled, lodNo, shadowLodNo);
#if !SHADER_API_GLCORE && !SHADER_API_GLES3 && !SHADER_API_VULKAN
        if (animateCrossFade)
        {
            fadeLevel = gpuiInstanceLODData[id.x].w;
            cfLodNo = gpuiInstanceLODData[id.x].z;
            CalculateCFLODNoAnimate(gpuiInstanceLODData[id.x].x, lodNo, cfLodNo, fadeLevel);
        }
        else
            CalculateCFLODNo(size, lodNo, cfLodNo, fadeLevel);
#endif
    }

    gpuiInstanceLODData[id.x].x = lodNo;
    gpuiInstanceLODData[id.x].y = shadowLodNo;
    gpuiInstanceLODData[id.x].z = cfLodNo;
    gpuiInstanceLODData[id.x].w = fadeLevel;
}
